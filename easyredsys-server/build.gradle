apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'


sourceCompatibility = 1.8

dependencies {

    compile project(':easyredsys-client')

    // Moxy is used to bind JAXB to JSON
    compile ('org.glassfish.jersey.media:jersey-media-moxy:2.22.2')

    // TODO - Ver si sustituir la librería anterior por esta otra
    //compile group: 'org.eclipse.persistence', name: 'org.eclipse.persistence.moxy', version: '2.6.3'

    // Web service axis redsys
    compile 'org.apache.axis:axis:1.4'
    compile 'org.apache.axis:axis-jaxrpc:1.4'

    // JAX-RS 2.0(.1)
    compile 'javax.ws.rs:javax.ws.rs-api:2.0.1'

    // Servlet 3
    compile 'javax.servlet:javax.servlet-api:3.1.0'

    compile group: 'org.glassfish.gmbal', name: 'gmbal', version: '4.0.0-b001'

    compile group: 'org.reflections', name: 'reflections', version: '0.9.10'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    tasks.javadoc.failOnError = false
    from tasks.javadoc.destinationDir
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }

            pom.project {
                name 'easyredsys-server'
                packaging 'jar'
                description "Librería que facilita la integración con la pasarela de pago Redsys para aplicaciones Java"
                url "https://www.miguelangeljulvez.com/easyredsys"

                scm {
                    url "https://github.com/majulvez/easyredsys"
                    connection "scm:git@github.com:majulvez/easyredsys.git"
                    developerConnection "scm:git@github.com:majulvez/easyredsys.git"
                }

                licenses {
                    license {
                        name "The Apache Software License, Version 2.0"
                        url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution "repo"
                    }
                }

                developers {
                    developer {
                        id "majulvez"
                        name "Miguel Ángel Júlvez"
                    }
                }
            }
        }
    }
}